buildscript {
    dependencies {
        classpath("net.serenity-bdd:serenity-gradle-plugin:2.0.11")
    }
}

plugins {
  id 'application'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id "info.solidsoft.pitest" version '1.5.2'
  id 'org.springframework.boot' version '2.7.0'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'uk.gov.hmcts.java' version '0.12.5'
  id 'org.owasp.dependencycheck' version '7.1.1'
  id 'com.github.ben-manes.versions' version '0.28.0'
  id 'org.sonarqube' version '3.3'
  id 'au.com.dius.pact' version '4.1.0'

}

apply plugin: 'net.serenity-bdd.aggregator'
group = 'uk.gov.hmcts.reform'
version = '0.0.1'

allprojects {
  sourceCompatibility = 11
  targetCompatibility = 11
}

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }

  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }

  contractTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/contractTest/java')
    }
    resources.srcDir file('src/contractTest/resources')
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

test {
  useJUnitPlatform()
  failFast = true
}

task functional(type: Test) {
  description = "Runs functional tests"
  group = "Verification"

  systemProperty "scenario", System.getProperty('scenario')

  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
  finalizedBy aggregate
}

task integration(type: Test) {
  description = "Runs integration tests"
  group = "Verification"
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  failFast = true
}

task smoke(type: Test) {
  description = "Runs Smoke Tests"
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
}

checkstyle {
  maxWarnings = 0
  toolVersion = '8.29'
  getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

pmd {
  toolVersion = "6.18.0"
  ignoreFailures = true
  sourceSets = [sourceSets.main, sourceSets.test, sourceSets.functionalTest, sourceSets.integrationTest, sourceSets.smokeTest]
  reportsDir = file("$project.buildDir/reports/pmd")
  // https://github.com/pmd/pmd/issues/876
  ruleSets = []
  ruleSetFiles = files("config/pmd/ruleset.xml")
}

project.tasks['sonarqube'].dependsOn test, integration

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.enabled = true
    csv.enabled = false
    xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport

pitest {
  junit5PluginVersion = '0.12'
  targetClasses = ['uk.gov.hmcts.reform.iacasepaymentsapi.*']
  excludedClasses = [
    'uk.gov.hmcts.reform.iacasepaymentsapi.infrastructure.config.*',
    'uk.gov.hmcts.reform.iacasepaymentsapi.Application',
    'uk.gov.hmcts.reform.iacasepaymentsapi.CcdScenarioRunnerTest'
  ]
  threads = 10
  outputFormats = ['XML', 'HTML']
  timestampedReports = false
  failWhenNoMutations = false
}

project.tasks['pitest'].group = "Verification"

sonarqube {
  properties {
    property "sonar.projectName", "IA :: Immigration & Asylum Payments API"
    property "sonar.projectKey", "IACASEPAYMENTSAPI"
    property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.destination.path}"
    property "sonar.pitest.mode", "reuseReport"
    property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    property "sonar.exclusions", "src/main/java/uk/gov/hmcts/reform/iacasepaymentsnapi/infrastructure/config/**," +
                                 "src/main/java/uk/gov/hmcts/reform/iacasepaymentsnapi/Application.java"
  }
}

project.ext {
  pacticipant = 'ia_casePaymentsApi'
  pacticipantVersion = getCheckedOutGitCommitHash()
  set('springCloudVersion', "2021.0.3")
}

def getCheckedOutGitCommitHash() {
  'git rev-parse --verify --short HEAD'.execute().text.trim()
}

task runAndPublishConsumerPactTests(type: Test, description: 'Publish pact test to broker.', group: 'Verification') {
  onlyIf {
    return System.env.SKIP_CONTRACT_TESTS == null || System.env.SKIP_CONTRACT_TESTS == 'false'
  }
  useJUnitPlatform()
  logger.lifecycle("Runs pact consumer Tests")
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  systemProperty 'pact.rootDir', "pacts"
  classpath = sourceSets.contractTest.runtimeClasspath
}

runAndPublishConsumerPactTests.finalizedBy pactPublish

pact {
  broker {
    pactBrokerUrl = System.getenv("PACT_BROKER_FULL_URL") ?: 'http://localhost:80'
  }
  publish {
    pactDirectory = 'pacts'
    tags = [System.getenv("PACT_BRANCH_NAME") ?:'Dev']
    version = project.pacticipantVersion
  }
}
// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

dependencyCheck {
  suppressionFile = 'config/owasp/suppressions.xml'

  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
}

dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
  }
}

repositories {
  jcenter()
  mavenCentral()
}

def testVersions = [
  springBoot      : '2.6.3',
  pitest          : '1.5.2',
  junit           : '5.6.2',
  junitPlatform   : '1.8.2',
  pact_version    : '4.1.7',
  serenity        : '3.1.20'
]

ext.libraries = [
  junit5: [
    "org.junit.jupiter:junit-jupiter-api:${testVersions.junit}",
    "org.junit.jupiter:junit-jupiter-engine:${testVersions.junit}",
    "org.junit.jupiter:junit-jupiter-params:${testVersions.junit}",
    "org.junit.platform:junit-platform-commons:${testVersions.junitPlatform}",
    "org.junit.platform:junit-platform-engine:${testVersions.junitPlatform}"
  ]
]

configurations {
  integrationTestCompile.extendsFrom testCompile
  integrationTestRuntime.extendsFrom testRuntime
  functionalTestCompile.extendsFrom testCompile
  functionalTestRuntime.extendsFrom testRuntime
}

dependencies {
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-oauth2-client'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-oauth2-resource-server'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'

  implementation group: 'uk.gov.hmcts.reform', name: 'logging', version: '5.1.7'
  implementation group: 'uk.gov.hmcts.reform', name: 'logging-appinsights', version: '5.1.7'
  implementation group: 'uk.gov.hmcts.reform', name: 'properties-volume-spring-boot-starter', version: '0.0.4'
  compile group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: '3.1.2'
  implementation group: 'uk.gov.hmcts.reform.auth', name: 'auth-checker-lib', version: '2.1.4'

  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign'

  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind'
  implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310'
  implementation group: 'org.apache.httpcomponents', name: 'httpclient'
  implementation group: 'org.apache.commons', name: 'commons-lang3'

  implementation group: 'commons-io', name: 'commons-io', version: '2.10.0'
  implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.6.9'
  implementation group: 'com.google.guava', name: 'guava', version: '31.1-jre'

  annotationProcessor 'org.projectlombok:lombok'
  compileOnly 'org.projectlombok:lombok'

  testImplementation libraries.junit5
  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: testVersions.springBoot, {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    exclude group: "com.vaadin.external.google", module: "android-json"
  }

  testCompile 'com.github.tomakehurst:wiremock:2.27.2'
  testCompile group: 'org.mockito', name: 'mockito-core', version: '3.5.0'
  testCompile group: 'org.powermock', name: 'powermock-api-mockito2', version: '2.0.7'
  testCompile 'com.github.tomakehurst:wiremock:2.27.2'
  testCompile "ru.lanwen.wiremock:wiremock-junit5:1.3.1"

  testImplementation group: 'io.rest-assured', name: 'rest-assured', version: '4.3.1'
  testCompile group: 'net.serenity-bdd', name: 'serenity-core', version: testVersions.serenity
  testCompile group: 'net.serenity-bdd', name: 'serenity-junit', version: testVersions.serenity
  testCompile group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: testVersions.serenity
  testCompile group: 'net.serenity-bdd', name: 'serenity-spring', version: testVersions.serenity

  testImplementation group: 'org.springframework.security', name: 'spring-security-test'

  testImplementation group: 'org.pitest', name: 'pitest', version: testVersions.pitest
  testImplementation group: 'info.solidsoft.gradle.pitest', name: 'gradle-pitest-plugin', version: '1.7.4'
  testImplementation group: 'org.codehaus.sonar-plugins', name: 'sonar-pitest-plugin', version: '0.5'
  testImplementation group: 'nl.jqno.equalsverifier', name: 'equalsverifier', version: '3.7.2'

  integrationTestImplementation sourceSets.main.runtimeClasspath
  integrationTestImplementation sourceSets.test.runtimeClasspath

  functionalTestImplementation sourceSets.main.runtimeClasspath
  functionalTestImplementation sourceSets.test.runtimeClasspath

  smokeTestImplementation sourceSets.main.runtimeClasspath
  smokeTestImplementation sourceSets.test.runtimeClasspath

  contractTestCompile group: 'au.com.dius.pact.consumer', name: 'junit5', version: testVersions.pact_version
  contractTestCompile group: 'au.com.dius.pact.consumer', name: 'java8', version: testVersions.pact_version
  contractTestCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: testVersions.springBoot
  contractTestCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: testVersions.junit_jupiter
  contractTestRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: testVersions.junit_jupiter
  contractTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: testVersions.junit_jupiter
  contractTestImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind'
  contractTestImplementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign'
}

mainClassName = 'uk.gov.hmcts.reform.iacasepaymentsapi.Application'

bootJar {
  getArchiveFileName().set(provider {
    'ia-case-payments-api.jar'
  })

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}
