buildscript {
  dependencies {
    classpath("net.serenity-bdd:serenity-gradle-plugin:3.6.22")
    classpath("au.com.dius.pact:au.com.dius.pact.gradle.plugin:4.4.0-beta.1")
  }
}

plugins {
  id 'application'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id "info.solidsoft.pitest" version '1.15.0'
  id 'io.spring.dependency-management' version '1.1.4'
  id 'org.springframework.boot' version '2.7.18'
  id 'uk.gov.hmcts.java' version '0.12.48'
  id 'org.owasp.dependencycheck' version '10.0.3'
  id 'com.github.ben-manes.versions' version '0.50.0'
  id 'org.sonarqube' version '4.3.0.3225'
  id 'au.com.dius.pact' version '4.2.14'
  id 'com.adarshr.test-logger' version '3.2.0'
}

apply plugin: 'java'
apply plugin: 'net.serenity-bdd.serenity-gradle-plugin'
apply plugin: 'project-report'
apply plugin: 'idea'

group = 'uk.gov.hmcts.reform'
version = '0.0.1'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }

  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }

  contractTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/contractTest/java')
    }
    resources.srcDir file('src/contractTest/resources')
  }
}

processFunctionalTestResources {
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

processIntegrationTestResources {
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

processContractTestResources {
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

test {
  useJUnitPlatform()
  failFast = true
}

task functional(type: Test, description: 'Runs the functional tests.', group: 'Verification') {
  systemProperty "scenario", System.getProperty('scenario')

  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
  finalizedBy aggregate
  testLogging {
    showStandardStreams = true
    showCauses = true
    showExceptions = true
    showStackTraces = true
  }
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  failFast = true
}

task smoke(type: Test) {
  description = "Runs Smoke Tests"
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
}

task contract(type: Test, description: 'Runs the pact contract tests.', group: 'Verification') {
  description = "Runs the consumer Pact tests"
  useJUnitPlatform()
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
}

task fortifyScan(type: JavaExec, description: 'Run fortify scan.')  {
  mainClass = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

checkstyle {
  maxWarnings = 0
  toolVersion = '10.12.0'
  getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

pmd {
  toolVersion = "6.55.0"
  ignoreFailures = true
  sourceSets = [sourceSets.main, sourceSets.test, sourceSets.functionalTest, sourceSets.integrationTest, sourceSets.smokeTest]
  reportsDir = file("$project.buildDir/reports/pmd")
  // https://github.com/pmd/pmd/issues/876
  ruleSets = []
  ruleSetFiles = files("config/pmd/ruleset.xml")
}

project.tasks['sonarqube'].dependsOn test, integration

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.required.set(true)
    csv.required.set(false)
    xml.outputLocation.set(layout.buildDirectory.file("reports/jacoco/test/jacocoTestReport.xml"))
  }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport

pitest {
  junit5PluginVersion = '1.1.2'
  targetClasses = ['uk.gov.hmcts.reform.iacasepaymentsapi.*']
  excludedClasses = [
    'uk.gov.hmcts.reform.iacasepaymentsapi.infrastructure.config.*',
    'uk.gov.hmcts.reform.iacasepaymentsapi.Application',
    'uk.gov.hmcts.reform.iacasepaymentsapi.CcdScenarioRunnerTest'
  ]
  threads = 10
  outputFormats = ['XML', 'HTML']
  timestampedReports = true
  failWhenNoMutations = false
  verbose = true
  mutationThreshold = 82
}

project.tasks['pitest'].group = "Verification"

sonarqube {
  properties {
    property "sonar.projectName", "IA :: Immigration & Asylum Payments API"
    property "sonar.projectKey", "IACASEPAYMENTSAPI"
    property "sonar.coverage.jacoco.xmlReportPaths", layout.buildDirectory.file("reports/jacoco/test/jacocoTestReport.xml").get().asFile
    property "sonar.pitest.mode", "reuseReport"
    property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    property "sonar.exclusions", "src/main/java/uk/gov/hmcts/reform/iacasepaymentsnapi/infrastructure/config/**," +
                                 "src/main/java/uk/gov/hmcts/reform/iacasepaymentsnapi/Application.java"
  }
}

project.ext {
  pacticipant = 'ia_casePaymentsApi'
  if (System.env.GIT_COMMIT) {
    pacticipantVersion = System.env.GIT_COMMIT.substring(0,9)
  } else {
    pacticipantVersion = getCheckedOutGitCommitHash()
  }
  set('springCloudVersion', "2021.0.8")
}

def getCheckedOutGitCommitHash() {
  'git rev-parse --verify --short HEAD'.execute().text.trim()
}


task runAndPublishConsumerPactTests(type: Test, description: 'Publish pact test to broker.', group: 'Verification') {
  onlyIf {
    return System.env.SKIP_CONTRACT_TESTS == null || System.env.SKIP_CONTRACT_TESTS == 'false'
  }
  useJUnitPlatform()
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
  systemProperty 'pact.rootDir', "pacts"
  testLogging.showStandardStreams = true
}

runAndPublishConsumerPactTests.finalizedBy pactPublish

pact {
  broker {
    pactBrokerUrl = System.getenv("PACT_BROKER_FULL_URL") ?: 'http://localhost:80'
  }
  publish {
    pactDirectory = 'pacts'
    tags = [System.getenv("PACT_BRANCH_NAME") ?:'Dev']
    version = project.pacticipantVersion + '_' + System.nanoTime().toString()
  }
}
// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

dependencyCheck {
  suppressionFile = 'config/owasp/suppressions.xml'

  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
}


def versions = [
  pitest          : '1.15.3',
  gradlePitest    : '1.15.0',
  junit           : '5.10.1',
  junitPlatform   : '1.10.1',
  reformLogging   : '5.1.7',
  springDoc       : '1.7.0',
  pact_version    : '4.1.38',
  serenity        : '3.9.8',
  sonarPitest     : '0.5',
  restAssured     : '5.3.2'
]


dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
  }
  dependencies {
    // CVE-2018-10237 - Unbounded memory allocation
    dependencySet(group: 'com.google.guava', version: '32.1.2-jre') {
      entry 'guava'
    }

    dependencySet(group: 'commons-fileupload', version: '1.6.0') {
      entry 'commons-fileupload'
    }

    dependencySet(group: 'com.fasterxml.jackson.core', version: '2.16.0') {
      entry 'jackson-databind'
      entry 'jackson-core'
      entry 'jackson-annotations'
    }

    dependencySet(group: 'com.fasterxml.jackson.dataformat', version: '2.16.0') {
      entry 'jackson-dataformat-yaml'
    }

    dependencySet(group: 'com.fasterxml.jackson.datatype', version: '2.16.0') {
      entry 'jackson-datatype-jsr310'
      entry 'jackson-datatype-jdk8'
    }

    dependencySet(group: 'com.fasterxml.jackson.module', version: '2.16.0') {
      entry 'jackson-module-parameter-names'
      entry 'jackson-module-afterburner'
    }

    dependency group: 'org.yaml', name: 'snakeyaml', version: '2.2'
  }
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url 'https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1'
  }
}

ext.libraries = [
  junit5: [
    "org.junit.jupiter:junit-jupiter-api:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-engine:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-params:${versions.junit}",
    "org.junit.platform:junit-platform-commons:${versions.junitPlatform}",
    "org.junit.platform:junit-platform-engine:${versions.junitPlatform}"
  ]
]

idea {
  module {
    testSourceDirs += project.sourceSets.integrationTest.java.srcDirs
    testResourceDirs += project.sourceSets.integrationTest.resources.srcDirs
    testSourceDirs += project.sourceSets.functionalTest.java.srcDirs
    testResourceDirs += project.sourceSets.functionalTest.resources.srcDirs
    testSourceDirs += project.sourceSets.smokeTest.java.srcDirs
    testResourceDirs += project.sourceSets.smokeTest.resources.srcDirs
    testSourceDirs += project.sourceSets.contractTest.java.srcDirs
    testResourceDirs += project.sourceSets.contractTest.resources.srcDirs
  }
}

configurations {
  integrationTestImplementation.extendsFrom testImplementation
  integrationTestCompile.extendsFrom testCompile
  integrationTestRuntime.extendsFrom testRuntime
  functionalTestImplementation.extendsFrom testImplementation
  functionalTestCompile.extendsFrom testCompile
  functionalTestRuntime.extendsFrom testRuntime
  contractTestImplementation.extendsFrom testImplementation
  contractTestCompile.extendsFrom testCompile
  contractTestRuntime.extendsFrom testRuntime
}

dependencies {

  modules {
    module("org.codehaus.groovy:groovy") {
      replacedBy("org.apache.groovy:groovy", "conflicts in current rest-assured version")
    }
    module("org.codehaus.groovy:groovy-xml") {
      replacedBy("org.apache.groovy:groovy-xml", "conflicts in current rest-assured version")
    }
    module("org.codehaus.groovy:groovy-json") {
      replacedBy("org.apache.groovy:groovy-json", "conflicts in current rest-assured version")
    }
  }
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-oauth2-client'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-oauth2-resource-server'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'

  implementation(group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap', version: '3.1.7') {
    exclude group: "org.bouncycastle", module: "bcprov-jdk15on"
  }
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-dependencies', version: '2021.0.8'
  implementation(group: 'org.springframework.cloud', name: 'spring-cloud-starter-netflix-hystrix', version: '2.2.10.RELEASE') {
    exclude group: "org.bouncycastle", module: "bcprov-jdk15on"
  }

  implementation group: 'org.springframework.security', name: 'spring-security-oauth2-client'
  implementation group: 'org.springframework.security', name: 'spring-security-oauth2-resource-server'
  implementation group: 'org.springframework.security', name: 'spring-security-oauth2-jose'

  implementation group: 'org.springframework', name: 'spring-aspects', version: '6.0.14'
  implementation group: 'org.springframework.retry', name: 'spring-retry', version: '2.0.2'

  implementation group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: '9.37.3'

  // CVE-2021-27568
  implementation group: 'net.minidev', name: 'json-smart', version: '2.5.2'

  implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: versions.springDoc

  implementation group: 'com.sun.xml.bind', name: 'jaxb-osgi', version: '2.3.9'

  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core'
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations'
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind'
  implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310'

  implementation group: 'jakarta.servlet', name: 'jakarta.servlet-api', version: '5.0.0'

  implementation group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '8.0'
  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: '6.0.1'
  implementation group: 'com.github.hmcts.java-logging', name: 'logging-appinsights', version: '6.0.1'

  implementation(group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '4.0.2') {
    exclude group: "org.bouncycastle", module: "bcprov-jdk15on"
  }
  implementation group:  'org.springdoc', name: 'springdoc-openapi-ui', version: versions.springDoc

  implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.13'
  implementation group: 'ch.qos.logback', name: 'logback-core', version: '1.2.13'

  implementation group: 'com.github.hmcts', name: 'auth-checker-lib', version: '3.0.2'

  implementation group: 'org.bouncycastle', name: 'bcprov-jdk18on', version: '1.78'

  implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.14'

  implementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-core', version: '9.0.106'
  implementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-websocket', version: '9.0.106'

  implementation group: 'commons-io', name: 'commons-io', version: '2.15.1'

  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-cache'

  implementation group: 'com.github.ben-manes.caffeine', name: 'caffeine'

  annotationProcessor 'org.projectlombok:lombok:1.18.30'
  implementation 'org.projectlombok:lombok:1.18.30'

  testImplementation group: 'org.springframework.security', name: 'spring-security-test'

  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'

  testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.12.4'
  testImplementation group: 'org.powermock', name: 'powermock-api-mockito2', version: '2.0.9'

  testImplementation(group: 'org.springframework.boot', name: 'spring-boot-starter-test') {
    exclude group: "com.vaadin.external.google", module: "android-json"
  }

  testImplementation group: 'org.apiguardian', name: 'apiguardian-api', version: '1.1.2'
  testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.12.4'
  testImplementation group: 'io.rest-assured', name: 'rest-assured-all', version: versions.restAssured
  testImplementation group: 'io.rest-assured', name: 'rest-assured', version: versions.restAssured
  testImplementation group: 'net.serenity-bdd', name: 'serenity-core', version: versions.serenity
  testImplementation group: 'net.serenity-bdd', name: 'serenity-junit', version: versions.serenity
  testImplementation group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenity
  testImplementation group: 'net.serenity-bdd', name: 'serenity-spring', version: versions.serenity


  testImplementation group: 'com.github.hmcts', name: 'fortify-client', version: '1.3.0-azure', classifier: 'all', {
    exclude group: 'commons-io', module: 'commons-io'
    exclude group: 'org.apache.commons', module: 'commons-lang3'
  }

  testImplementation group: 'org.pitest', name: 'pitest', version: versions.pitest
  testImplementation "info.solidsoft.gradle.pitest:gradle-pitest-plugin:${versions.gradlePitest}"
  testImplementation "org.codehaus.sonar-plugins:sonar-pitest-plugin:${versions.sonarPitest}"

  testImplementation group: 'nl.jqno.equalsverifier', name: 'equalsverifier', version: '3.7.2'

  testImplementation(group: 'org.springframework.cloud', name: 'spring-cloud-contract-wiremock', version: '3.1.8') {
    exclude group: "com.github.tomakehurst", module: "wiremock-standalone"
  }

  integrationTestImplementation sourceSets.main.runtimeClasspath
  integrationTestImplementation sourceSets.test.runtimeClasspath

  functionalTestImplementation sourceSets.main.runtimeClasspath
  functionalTestImplementation sourceSets.test.runtimeClasspath

  smokeTestImplementation sourceSets.main.runtimeClasspath
  smokeTestImplementation sourceSets.test.runtimeClasspath

  contractTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
  contractTestImplementation libraries.junit5
  contractTestImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-core'
  contractTestImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind'
  contractTestImplementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310'


  contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'junit5', version: versions.pact_version
  contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'java8', version: versions.pact_version

  contractTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
  contractTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit

  contractTestRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-commons', version: '1.10.1'

  contractTestImplementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '3.1.8'
  contractTestImplementation group: 'org.springframework.cloud', name: 'spring-cloud-netflix-ribbon', version: '2.2.10.RELEASE'
}

mainClassName = 'uk.gov.hmcts.reform.iacasepaymentsapi.Application'

bootJar {
  getArchiveFileName().set(provider {
    'ia-case-payments-api.jar'
  })

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}

testlogger {
  theme 'standard'
  showExceptions true
  showStackTraces true
  showFullStackTraces false
  showCauses true
  slowThreshold 5000
  showSummary true
  showSimpleNames false
  showPassed true
  showSkipped true
  showFailed true
  showStandardStreams false
  showPassedStandardStreams true
  showSkippedStandardStreams true
  showFailedStandardStreams true
}
